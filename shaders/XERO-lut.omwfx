uniform_bool uEnabled {
	default = true;
}

uniform_bool flipV {
	default = true;
}

sampler_2d SamplerLUT {
	source = "textures/XERO-lut.tga";
	mag_filter = linear;
	min_filter = linear;
}

shared {
	vec3 saturate(vec3 value) {
		return clamp(value, 0.0, 1.0);
	}
	
	vec3 ClutFunc(vec3 colorIN, sampler2D LutSampler) {
		vec2 CLut_pSize = vec2(0.00390625, 0.0625);
		vec4 CLut_UV;
		colorIN = saturate(colorIN) * 15.0;
		CLut_UV.w = floor(colorIN.b);
		CLut_UV.xy = (colorIN.rg + 0.5) * CLut_pSize;
		CLut_UV.x += CLut_UV.w * CLut_pSize.y;
		CLut_UV.z = CLut_UV.x + CLut_pSize.y;
		if (flipV) {
			CLut_UV.y = 1.0 - CLut_UV.y;
		}
		return mix(omw_Texture2D(LutSampler, CLut_UV.xy).rgb, omw_Texture2D(LutSampler, CLut_UV.zy).rgb, colorIN.b - CLut_UV.w);
	}
	
}

fragment main {
	omw_In vec2 omw_TexCoord;
	

	void main()
	{
		
		vec3 scene = omw_GetLastShader(omw_TexCoord).rgb;
		
		if (uEnabled) {
			vec4 col = vec4(ClutFunc(scene, SamplerLUT), 1.0);
			omw_FragColor = col;
		} 
		else {
			omw_FragColor = vec4(scene, 1.0);
		}
	}
}

technique {
    description = "XERO-LUT ported from MGEXE";
    passes = main;
    version = "1.0";
    author = "Epoch";
}